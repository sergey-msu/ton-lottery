"TonUtil.fif" include
"Asm.fif" include

// ------------------------------------------- CODE

"out/lottery-code.fif" include
<s constant code

// ----------------------------------------- CONSTS

0 tuple 0x076ef1ea , // magic
0 , 0 , // actions msg_sents
1570998536 , // unix_time
1 , 1 , 3 , // block_lt, trans_lt, rand_seed
0 tuple 100000000000000 , dictnew , , // remaining balance
0 , dictnew , // contract_address, global_config
1 tuple // wrap to another tuple
constant c7

true constant bounce

0 constant recv_internal  // to run recv_internal()
-1 constant recv_external // to invoke recv_external()


// --------------------------------------- VARS

variable storage
variable message
variable exit_code

variable owner_public_key
variable owner_private_key
variable other_private_key
variable orders
variable owner_wc
variable owner_account_id
variable message_to_sign
variable message_to_send
variable signature


// --------------------------------------- TEST #1. Empty message

cr .">>>>>>>>>>>>>>>>>>>>> TEST #1. Empty message >>>>>>>>>>>>>>>>>>>>>" cr cr

<b b> storage !
<b b> <s message !

message @ 
recv_external 
code 
storage @ 
c7 
runvmctx


// --------------------------------------- TEST #2. Multiple calls error

cr .">>>>>>>>>>>>>>>>>>>>> TEST #2. Multiple calls error >>>>>>>>>>>>>>>>>>>>>" cr cr

"./tests/keys/owner.pk" file>B owner_private_key !
owner_private_key @ priv>pub owner_public_key !

dictnew orders !
0 owner_wc !
0 owner_account_id !

<b 166 32 u, owner_public_key @ B, 0 32 u, 0 32 u, 0 Gram, 0 Gram, owner_wc @ 32 i, owner_account_id @ 256 u,  orders @ dict, b> storage !
<b 165 32 u, b> message !

message @ hashu owner_private_key @ ed25519_sign_uint signature !
<b signature @ B, 165 32 u, b> <s message_to_send !  

message_to_send @ 
recv_external 
code
storage @ 
c7 
runvmctx

drop 
exit_code ! 
."Exit code " exit_code @ . cr 

exit_code @ 33 - abort"Test #2 Not passed"


// --------------------------------------- TEST #3. Signature check

cr .">>>>>>>>>>>>>>>>>>>>> TEST #3. Signature check >>>>>>>>>>>>>>>>>>>>>" cr cr

// build SM context
<b 0 32 u, owner_public_key @ B, 0 32 u, 0 32 u, 0 Gram, 0 Gram, owner_wc @ 32 i, owner_account_id @ 256 u,  orders @ dict, b> storage !

// build message to send
<b 0 32 u, 0 7 u, b> message_to_sign !
message_to_sign @ hashu owner_private_key @ ed25519_sign_uint signature !
<b signature @ B, 0 32 u, 0 7 u, b> <s  message_to_send !

// run test
message_to_send @ 
recv_external 
code 
storage @
c7
runvmctx

storage ! 
exit_code ! 
."Exit code " exit_code @ . cr 


// --------------------------------------- TEST #4. Wrong signature provides 34 error

cr .">>>>>>>>>>>>>>>>>>>>> TEST #4. Wrong signature provides 34 error >>>>>>>>>>>>>>>>>>>>>" cr cr

//  build SM context 
<b 0 32 u, owner_public_key @ B, 0 32 u, 0 32 u, 0 Gram, 0 Gram, owner_wc @ 32 i, owner_account_id @ 256 u,  orders @ dict, b> storage !

// build message to send
<b 0 32 u, b> message_to_sign !


"./tests/keys/other.pk" file>B other_private_key !

message_to_sign @ hashu other_private_key @ ed25519_sign_uint signature !
<b signature @ B, 0 32 u, b> <s  message_to_send ! 

message_to_send @ 
recv_external 
code 
storage @
c7
runvmctx

storage ! 
exit_code ! 
."Exit code " exit_code @ . cr 

exit_code @ 34 - abort"Test #4 not passed"


// --------------------------------------- TEST #5. Change SM owner

cr .">>>>>>>>>>>>>>>>>>>>> TEST #5. Change SM owner >>>>>>>>>>>>>>>>>>>>>" cr cr

// create sm context
variable new_owner_wc 
variable new_owner_account_id

0 owner_wc !
0 owner_account_id !
-2 new_owner_wc !
111 new_owner_account_id !

<b 0 32 u, owner_public_key @ B, 0 32 u, 0 32 u, 0 Gram, 0 Gram, owner_wc @ 32 i, owner_account_id @ 256 u, orders @ dict, b> storage ! 
<b 0 32 u, 1 7 u, new_owner_wc @  32 i, new_owner_account_id @ 256 u, b> message_to_sign !
message_to_sign @ hashu owner_private_key @ ed25519_sign_uint signature !

<b signature @ B, 0 32 u, 1 7 u, new_owner_wc @ 32 i, new_owner_account_id @ 256 u, b> <s  message_to_send !

message_to_send @ 
recv_external 
code
storage @
c7
runvmctx


storage !
exit_code !
."Exit code " exit_code @ . cr
35 exit_code @ - 0 = 
.s
abort"Message bits mismatch"

// parse storage to check if address changed

variable stored_slice 
variable stored_seqno
variable stored_pubkey 
variable stored_order_seqno
variable number_of_wins 
variable incoming_amount
variable outgoing_amount 
variable stored_wc 
variable stored_account_id

storage @ <s stored_slice ! 
stored_slice @ 32 u@+ stored_slice ! 
stored_seqno !
stored_slice @ 32 B@+ stored_slice ! 
stored_pubkey ! 
stored_slice @ 32 u@+ stored_slice ! 
stored_order_seqno ! 
stored_slice @ 32 u@+ stored_slice ! 
number_of_wins ! 
stored_slice @ Gram@+ stored_slice ! 
incoming_amount !
stored_slice @ Gram@+ stored_slice !
outgoing_amount !
stored_slice @ 32 i@+ stored_slice ! // read wc 
stored_wc ! 
stored_slice @ 256 u@+ stored_slice ! // read account_id 
stored_account_id ! 


stored_wc @ new_owner_wc @ = 
variable is_wc_equal 
is_wc_equal ! 
is_wc_equal @ 1 +
abort"Test #5 failed. Reason: stored wc is different from target"

stored_account_id @ new_owner_account_id @ = 
variable is_account_id_eq
is_account_id_eq !
is_account_id_eq @ 1 +
abort"Test #5 failed. Reason: stored account_id is different from target" 


// --------------------------------------- TEST #6.1. Try send to owner more coins than available balance.

cr .">>>>>>>>>>>>>>>>>>>>> TEST #6.1. Try send to owner more coins than available balance. >>>>>>>>>>>>>>>>>>>>>" cr cr

<b 0 32 u, owner_public_key @ B, 0 32 u, 0 32 u, 0 Gram, 0 Gram, owner_wc @ 32 i, owner_account_id @ 256 u,  orders @ dict, b> storage ! 
<b 0 32 u, 2 7 u, 99999500000001 Gram, b> message_to_sign !   // 100000 coins - 0.5 coin = maximum to send, +1 nanocoin = insufficient
message_to_sign @ hashu owner_private_key @ ed25519_sign_uint signature !
<b signature @ B, 0 32 u, 2 7 u, 99999500000001 Gram, b> <s  message_to_send !


message_to_send @ 
recv_external 
code 
storage @
c7
runvmctx

storage ! 
exit_code !
."Exit code " exit_code @ . cr
36 exit_code @ - 0 = 1 +
.s
abort"Test #6.1 failed. Reason: not enough balance, but exception 36 is not thrown"


// --------------------------------------- TEST #6.2. Try send to owner correct number of coins.

cr .">>>>>>>>>>>>>>>>>>>>> TEST #6.2. Try send to owner correct number of coins. >>>>>>>>>>>>>>>>>>>>>" cr cr

<b 0 32 u, 2 7 u, 99999500000000 Gram, b> message_to_sign !   //    // 100000 coins - 0.5 coin = maximum to send
message_to_sign @ hashu owner_private_key @ ed25519_sign_uint signature !
<b signature @ B, 0 32 u, 2 7 u, 99999500000000 Gram, b> <s  message_to_send !

message_to_send @
recv_external
code
storage @
c7
runvmctx

storage ! 
exit_code !
."Exit code " exit_code @ . cr

exit_code @ 0 - abort"Test #6.2 not passed"


// --------------------------------------- Test #7. Just send some grams.

cr .">>>>>>>>>>>>>>>>>>>>> Test #7. Just send some grams. >>>>>>>>>>>>>>>>>>>>>" cr cr

<b 0 32 u, owner_public_key @ B, 0 32 u, 0 32 u, 0 Gram, 0 Gram, owner_wc @ 32 i, owner_account_id @ 256 u,  orders @ dict, b> storage ! 

"./tests/addresses/sender.addr" load-address 
2dup 2constant wallet_addr
."Contract address = " 2dup .addr cr 6 .Addr cr

<b b> <s message_to_send !

<b b{1000100} s, wallet_addr addr, 0 Gram, b{00} s, b> constant meta 

4999999991 
meta
message_to_send @ 
recv_internal 
code 
storage @
c7
runvmctx

storage ! 
exit_code ! 
."Exit code " exit_code @ . cr
